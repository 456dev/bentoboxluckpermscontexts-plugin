plugins {
    id("java")
    id("io.papermc.paperweight.userdev") version "1.5.5"
    id("xyz.jpenilla.run-paper") version "2.1.0"
    id("com.github.johnrengelman.shadow") version "8.1.1"
}

group = 'dev.the456gamer'
version = '1.0.0'
description = "BentoBox LuckPerms Contexts with custom flags"

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { // configurate yaml
        name = "githubThe456gamer"
        url = "https://maven.pkg.github.com/the456gamer/plugins"
        credentials(PasswordCredentials)
    }
    maven { url "https://repo.codemc.org/repository/maven-public/" }
}

dependencies {
    paperweight.paperDevBundle("1.20.1-R0.1-SNAPSHOT")
    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'world.bentobox:bentobox:1.24.1-SNAPSHOT'

    implementation 'com.github.the456gamer.custom.Configurate:yaml:4.2.0-SNAPSHOT'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion.set(JavaLanguageVersion.of(targetJavaVersion))
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
    options.encoding = "UTF-8"
}

tasks {
    // Configure reobfJar to run when invoking the build task
    assemble {
        dependsOn(reobfJar)
    }

    processResources {
        filteringCharset "UTF-8"
        def props = [
                "name"       : project.name,
                "version"    : project.version,
                "description": project.description,
                "apiVersion" : "1.20"
        ]

        inputs.properties(props)
        filesMatching("plugin.yml") {
            expand(props)
        }
        filesMatching('paper-plugin.yml') {
            expand props
        }
    }

    shadowJar {
        relocate 'org.spongepowered.configurate', 'dev.the456gamer.shadow.configurate'
        relocate 'io.leangen.geantyref', 'dev.the456gamer.shadow.geantyref'
        relocate 'com.github.the456gamer.custom.Configurate.configurate.yaml.internal.snakeyaml', 'dev.the456gamer.shadow.snakeyaml' //TODO
    }
}

